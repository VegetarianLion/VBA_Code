'*****************************************************************************************
'函数名：    GET_SQL_To_Arr
'函数功能：  获得指定SQL的查询结果，自定义连接字符串，可以连接各种数据库
'返回值：    返回一个二维数组
'参数1：     StrSQL   字符类型   SQL查询语句
'参数2：     Str_coon 字符类型   数据库连接语句
'Str_coon = "Provider=Microsoft.JET.OLEDB.4.0;Extended Properties='Excel 8.0;HDR=yes';Data Source=" & ThisWorkbook.FullName      '//OFFICE2003
'Str_coon = "Provider=Microsoft.ACE.OLEDB.12.0;Extended Properties='Excel 12.0;HDR=yes';Data Source =" & ThisWorkbook.FullName     '//OFFICE2007
'Str_coon = "HDR=yes';Data Source =" & FileArr(i)    '//OFFICE2003，2007 通用
'参数3：     Biaoti   可参数选   是否输出标题，默认带有标题
'使用方法：
'            SQLARR= GET_SQL_To_Arr(StrSQL,Str_coon,true)
'            SQLARR(0,1)  '//数组第一行为标题行，从i=1 开始是数据
'            Sh2.Range("A2").Resize(UBound(SQLARR, 1) + 1, UBound(SQLARR, 2) + 1) = SQLARR
'*****************************************************************************************
Public Function GET_SQL_To_Arr_Access(ByVal StrSQL As String, ByVal Str_coon As String, Optional Biaoti As Boolean = True) As Variant()
    'On Error Resume Next    ' 改变错误处理的方式。
    Dim CN, RS
    Dim arr()
    Dim i As Long
    
    Err.Clear
    Set CN = CreateObject("Adodb.Connection") '//新建一个ADO连接
    Set RS = CreateObject("adodb.recordset")
    Rem Str_coon = "HDR=yes';Data Source=" & ThisWorkbook.FullName
    If InStr(Str_coon, "Provider=") = 0 Then
        If Val(Application.Version * 1) < 12 Then
            Str_coon = "Provider=Microsoft.Jet.OLEDB.4.0;" & Str_coon
        Else
            Str_coon = "Provider=Microsoft.ACE.OLEDB.12.0;" & Str_coon
        End If
    End If
    CN.CursorLocation = 3
    CN.Open Str_coon
    RS.Open StrSQL, CN, 1, 3
    
    Rem 如果不要标题，可以：arr = RS.GetRows，代码比较省，但是速度一般
    
    Rem SET RS=CN.Execute(StrSQL)
    If RS.RecordCount > 0 Then '//如果找到数据
        If Biaoti = True Then
            ReDim arr(0 To RS.RecordCount, 0 To RS.Fields.Count - 1)
            For a = 0 To RS.Fields.Count - 1  '//导入标题
                arr(0, a) = RS.Fields(a).Name
            Next
            For i = 0 To RS.RecordCount - 1  '//导入数据
                For a = 0 To RS.Fields.Count - 1
                    arr(i + 1, a) = RS.Fields(a).Value
                Next a
                RS.MoveNext
            Next
        Else
            ReDim arr(0 To RS.RecordCount - 1, 0 To RS.Fields.Count - 1)
            For i = 0 To RS.RecordCount - 1  '//导入数据
                For a = 0 To RS.Fields.Count - 1
                    arr(i, a) = RS.Fields(a).Value
                Next a
                RS.MoveNext
            Next
        End If
    Else '//如果没有找到数据
        If Biaoti = True Then
            ReDim arr(0 To 0, 0 To RS.Fields.Count - 1)
            For a = 0 To RS.Fields.Count - 1  '//导入标题
                arr(0, a) = RS.Fields(a).Name
            Next
        Else
            ReDim arr(0, 0)
            arr(0, 0) = ""
        End If
    End If
    
    If Err.Number <> 0 Then
        MsgBox "Error！" & Err.Description
        ReDim arr(0, 0)
        arr(0, 0) = "Error"
        GET_SQL_To_Arr_Access = arr(0, 0)
    End If
    
    GET_SQL_To_Arr_Access = arr
    
    RS.Close
    CN.Close  '//关闭ADO连接
    Set RS = Nothing
    Set CN = Nothing  '//释放内存
End Function
